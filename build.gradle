buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'com.diffplug.spotless' version '5.7.0'
  id "com.github.ben-manes.versions" version '0.34.0'
  id 'com.github.jk1.dependency-license-report' version '1.16'
  id 'com.github.spotbugs' version '4.5.1'
  id 'com.jfrog.bintray' version '1.8.5'
  id 'de.thetaphi.forbiddenapis' version '3.1'
  id 'jacoco'
  id 'org.owasp.dependencycheck' version '6.0.3'
  id 'net.researchgate.release' version '2.8.1'
}

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'com.github.jk1.dependency-license-report'
apply plugin: 'com.github.spotbugs'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.owasp.dependencycheck'

description 'Asset Manager'
mainClassName = 'am.app.App'
def licenseName = 'The Apache Software License, Version 2.0'

defaultTasks 'clean', 'check', 'javadoc', 'installDist', 'generateLicenseReport', 'run'

dependencies {
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
  implementation group: 'com.github.mjeanroy', name: 'exiftool-lib', version: '2.6.0'
  implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-repository-sparql', version: '3.4.3'
  implementation group: 'org.wikidata.wdtk', name: 'wdtk-wikibaseapi', version: '0.11.1'
  implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.32.3.2'
  testImplementation 'junit:junit:4.13.1'
}

// force versions of transitive dependencies
configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
   if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
      details.useVersion '29.0-jre'
      details.because 'CVE-2018-10237'
    }
  }
}

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = sourceCompatibility
  options.debugOptions.debugLevel = 'lines,vars,source'
  options.encoding = java.nio.charset.StandardCharsets.UTF_8
  options.warnings = true
  options.deprecation = true
  options.compilerArgs += ['-Xlint:all', '-Werror']
}

task createPomProperties() {
  description "create a text file to be included in the jar to make it self-describing"
  doLast {
    new File(buildDir, "pom.properties").text = """version=${version}
groupId=${project.group}
artifactId=${project.name}
"""
  }
}

def retrieveGitModification = { ->
  def baos = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'diff', '--shortstat'
    standardOutput = baos
  }
  return baos.toString().trim()
}

def retrieveGitRevision = { ->
  def baos = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', 'HEAD'
    standardOutput = baos
  }
  return baos.toString().trim()
}
def revision = retrieveGitRevision()
def modified = retrieveGitModification()
modified = !(modified == null || modified.trim().length() == 0)

def timeZone = java.util.TimeZone.getTimeZone(java.time.ZoneOffset.UTC)
def dateFormatter = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssZ", java.util.Locale.ENGLISH)
dateFormatter.setTimeZone(timeZone)
def now = dateFormatter.format(new Date())
def gradleVersion = getProject().getGradle().getGradleVersion()
jar {
  manifest {
    attributes(
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
      'Implementation-URL': 'https://github.com/marco-schmidt/am/',
      'Automatic-Module-Name': "${project.group}.${project.name}",
      'Created-By': 'Gradle ' + gradleVersion,
      'Build-Date': now,
      'License': licenseName,
      'Main-Class': mainClassName,
      'Java-Version': targetCompatibility,
      'Git-Revision': revision,
      'Git-Modified': modified,
      'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '))
  }
  from ('.') {
    include 'LICENSE'
    into 'META-INF'
  }
  into("META-INF/maven/${project.group}/${project.name}") {
    from { generatePomFileForLibraryPublication }
    rename ".*", "pom.xml"
  }
  from (buildDir) {
    include 'pom.properties'
    into "META-INF/maven/${project.group}/${project.name}"
  }
}
jar.dependsOn createPomProperties

run {
  args += ['--print-env', '-l', 'info']
}

forbiddenApis {
  bundledSignatures = ['jdk-system-out', 'jdk-unsafe', 'jdk-internal',
    'jdk-deprecated', 'jdk-non-portable', 'jdk-reflection' ]
  // https://jenkins.thetaphi.de/job/Forbidden-APIs/javadoc/signatures-syntax.html
  signaturesFiles = files('config/forbiddenApis/signatures.txt')
  ignoreFailures = false
}

// https://github.com/diffplug/spotless
spotless {
  java {
    licenseHeaderFile 'config/spotless/license-header.txt'
    removeUnusedImports()
    trimTrailingWhitespace()
    eclipse().configFile 'config/eclipse/formatter.xml'
  }
}

// https://github.com/checkstyle/checkstyle
// http://checkstyle.sourceforge.net
checkstyle {
  toolVersion '8.34'
  ignoreFailures false
  showViolations true
  configDirectory = file("${rootDir}/config/checkstyle")
}

// https://github.com/spotbugs/spotbugs
spotbugs {
  toolVersion = '4.0.6'
  effort = 'max' // min, default, max
  reportLevel = 'low' // low, medium, high  
  ignoreFailures = false
  excludeFilter = file('config/spotbugs/excludeBugsFilter.xml')
}

jacoco {
  toolVersion = "0.8.5"
}

jacocoTestReport {
  reports {
    xml.enabled true
    xml.destination file("${buildDir}/reports/jacoco/report.xml")
    csv.enabled false
    html.destination file("${buildDir}/reports/jacoco")
  }
}
jacocoTestCoverageVerification.dependsOn test
jacocoTestReport.dependsOn jacocoTestCoverageVerification

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
    html.stylesheet resources.text.fromFile('config/spotbugs/report-stylesheet.xsl')
  }
}

spotbugsTest {
  classes = files("${project.buildDir}/classes/java/main")
}

javadoc {
  options {
    windowTitle = "am ${project.version} (asset manager) API documentation"
    docTitle = '<a target="_top" href="https://github.com/marco-schmidt/am">am ' + project.version + ': asset manager</a>'
    linksOffline 'https://docs.oracle.com/javase/8/docs/api/', 'config/javadoc/'
  }
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task packageCoverageReport(type: Zip, dependsOn: 'jacocoTestReport') {
  description "create a zip archive with coverage report information"
  from jacocoTestReport.reports.html.destination
  classifier = 'coveragereport'
}

task packageLicenseReport(type: Zip, dependsOn: 'generateLicenseReport') {
  description "create a zip archive with license report information"
  from licenseReport.outputDir
  classifier = 'deplicensereport'
}

dependencyCheck {
  scanConfigurations = ['runtimeClasspath']
  cveValidForHours=24
  failBuildOnCVSS=0
  analyzers {
    assemblyEnabled=false
  }
  outputDirectory = "${buildDir}/reports/security-report"
}
check.dependsOn dependencyCheckAnalyze

// version is stable if it contains only numerical parts or a 'stable keyword'
def isStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase(Locale.ENGLISH).contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return stableKeyword || (version ==~ regex)
}

dependencyUpdates {
  rejectVersionIf {
    !isStable(it.candidate.version)
  }
  outputDir = "${buildDir}/reports/dependencyUpdates"
}

task packageDependencyCheckReport(type: Zip, dependsOn: 'dependencyCheckAnalyze') {
  description "create a zip archive with report on dependency vulnerability issues (as stored in NVD)"
  from dependencyCheck.outputDirectory
  include 'dependency-check-report.html'
  classifier = 'depvulnreport'
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/marco-schmidt/am")
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
        password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
      }
    }
  }
  publications {
    library(MavenPublication) {
      from components.java
      artifact packageSources
      artifact packageJavadoc
      artifact packageCoverageReport
      artifact packageDependencyCheckReport
      artifact packageLicenseReport
      groupId group
      pom.withXml {
        asNode().appendNode('name', project.name)
        asNode().appendNode('url', 'https://github.com/marco-schmidt/am')
        asNode().appendNode('description', description)
        asNode().appendNode('packaging', 'jar')
        asNode().appendNode('inceptionYear', '2019')
        asNode().appendNode('properties').
          appendNode('gitrevision', revision).parent().
          appendNode('gitmodified', modified).parent().
          appendNode('buildtime', now).parent().
          appendNode('gradleversion', gradleVersion).parent().
          appendNode('java', targetCompatibility)
        asNode().appendNode('scm').
          appendNode('connection', 'scm:git:git@github.com:marco-schmidt/am.git').parent().
          appendNode('developerConnection', 'scm:git:git@github.com:marco-schmidt/am.git').parent().
          appendNode('url', 'https://github.com/marco-schmidt/am')
        asNode().appendNode('developers').appendNode('developer').
          appendNode('id', 'marco-schmidt').parent().
          appendNode('name', 'Marco Schmidt').parent().
          appendNode('email', 'mschmidtgit@protonmail.com').parent().
          appendNode('organization', 'private').parent().
          appendNode('organizationUrl', 'https://github.com/marco-schmidt')
        asNode().appendNode('licenses').appendNode('license').
          appendNode('name', licenseName).parent().
          appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.html')
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['library']
  publish = true // auto-publish after upload
  pkg {
    repo = 'am'
    name = 'maven'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/marco-schmidt/am.git'
    publicDownloadNumbers = false
    version {
      name = project.version
      released  = new Date()
    }
  }
}
afterReleaseBuild.dependsOn = [bintrayUpload, publishLibraryPublicationToGitHubPackagesRepository]

licenseReport {
  allowedLicensesFile = new File("$projectDir/config/licenseReport/allowed-licenses.json")
}
check.dependsOn checkLicense

buildScan {
  termsOfServiceUrl = "https://gradle.com/terms-of-service"
  termsOfServiceAgree = "yes"
}
